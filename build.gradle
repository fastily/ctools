apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'eclipse'
apply plugin: 'idea'

version="0.0.1"
description="${project.name} build script"
group="fastily"

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    api 'commons-cli:commons-cli:1.4'
//  api 'org.json:json:20160810'

    api 'fastily:jwiki:1.3.2'
}

def pomConfig = {
    licenses {
        license {
            name 'GNU General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/gpl-3.0.txt'
            distribution 'repo'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom.withXml {
                asNode().children().last() + pomConfig
            }

        	artifact sourceJar
        	artifact packageJavadoc
       }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/javase/8/docs/api/"
        links "https://square.github.io/okio/1.x/okio/"
        links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"
        links "https://fastily.github.io/jwiki/docs/jwiki/"
        links "https://commons.apache.org/proper/commons-cli/javadocs/api-release/"

        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier = "sources"
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = "javadoc"
}

configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}